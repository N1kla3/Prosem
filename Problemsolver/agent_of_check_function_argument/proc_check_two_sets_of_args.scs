scp_program -> proc_check_two_sets_of_args(*

    ->rrel_params: .proc_check_two_sets_of_args_params(*
        ->rrel_1: rrel_in: _function_args;;
        ->rrel_2: rrel_in: _call_args;;
        ->rrel_3: rrel_out: _is_equal;;
    *);;
    
    ->rrel_operators: .proc_check_two_sets_of_args_operators_set(*
        ->rrel_init: .proc_check_two_sets_of_args_init(*
            <-searchElStr5;;
			->rrel_1:rrel_fixed:rrel_scp_var: _function_args;;
			->rrel_2:rrel_assign: rrel_scp_var: _deleted_arc;;
			->rrel_3:rrel_assign: rrel_scp_var: _func_type;;
			->rrel_4: rrel_assign: rrel_scp_var: _arc2;;
			->rrel_5: rrel_assign:rrel_scp_var: _relation;;
			=>nrel_then: .proc_check_two_sets_of_args_delete_arc;;
			=>nrel_else: .proc_check_two_sets_of_args_set_true;;
        *);;

        ->.proc_check_two_sets_of_args_delete_arc(*
            <-eraseEl;;
            ->rrel_1:rrel_fixed:rrel_erase:rrel_scp_var: _deleted_arc;;
            =>nrel_goto:.proc_check_two_sets_of_args_get_call_arg_type;;
        *);;

        ->.proc_check_two_sets_of_args_get_call_arg_type(*
            <-searchElStr5;;
			->rrel_1:rrel_fixed:rrel_scp_var: _call_args;;
			->rrel_2:rrel_assign: rrel_scp_var: _arc1;;
			->rrel_3:rrel_assign: rrel_scp_var: _call_type;;
			->rrel_4: rrel_assign: rrel_scp_var: _arc2;;
			->rrel_5: rrel_fixed:rrel_scp_var: _relation;;
			=>nrel_then: .proc_check_two_sets_of_args_check_types;;
			=>nrel_else: .proc_check_two_sets_of_args_set_false;;
        *);;

        //then
        ->.proc_check_two_sets_of_args_check_types(*
            <-ifCoin;;
            ->rrel_1:rrel_fixed:rrel_scp_var: _func_type;;
            ->rrel_2:rrel_fixed:rrel_scp_var: _call_type;;
            =>nrel_then:.proc_check_two_sets_of_args_init;;
            =>nrel_else:.proc_check_two_sets_of_args_set_false;;
        *);;
        //else
        ->.proc_check_two_sets_of_args_set_false(*
            <-varAssign;;
            ->rrel_1: rrel_assign: rrel_scp_var: _is_equal;;
            ->rrel_2: rrel_fixed: rrel_scp_const: [0];;
            =>nrel_goto: .proc_check_two_sets_of_args_operator_return;;
        *);;

        ->.proc_check_two_sets_of_args_set_true(*
            <-varAssign;;
            ->rrel_1: rrel_assign: rrel_scp_var: _is_equal;;
            ->rrel_2: rrel_fixed: rrel_scp_const: [1];;
            =>nrel_goto: .proc_check_two_sets_of_args_operator_return;;
        *);;

        ->.proc_check_two_sets_of_args_operator_return(*
            <-return;;
        *);;
    *);;
*);;